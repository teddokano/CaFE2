#
#   0から9までの4つの数字を1度ずつ+-*/の計算を行うことによって10を作り出すゲームの答えを求める．
#
#   これまで「ten_by_4nums.cafe」としていたファイルの計算方法を大幅に変えたバージョン
#   以前のものはスタックを一つだけ使って逐次的に計算していくものだったが，スタック操作関数としてsortとuniqを加えたのを契機に
#   これらの機能を活用したり，汎用的な関数を定義してそれを使うようにしてみた．
#
#	関数「make10」はスタック上の4つの数値をとり，「permutation」によってこの4つの数字でできる24通りのすべての順列を作り出す．
#   続いて演算子の組み合わせを「operation」によって作る
#   permutationとoperationの結果はそれぞれスタックに積まれているので，両方の内容から「combination」 で全ての組み合わせを作る
#   全ての非演算子と演算子の組み合わせを作り出し，それを評価．10になったものがあれはその計算式をスタックに積む．
#
#	関数「ten」はスタック上の4つの数値をとり，この順番に従った(数値の順番は入れ替えずに)計算を行う．
#   計算方法は上記の方法のpermutationを行わない版となっている．
#
#	このプログラムはcafeの可能性を確認する目的だけのために作成された．速度的な最適化は一切行われていない．
#
#	01-Sep-2006
#	23-Jun-2010  "-" "/" の演算順についての扱いのためswapを挿入することにした
#	30-Jan-2015  ユーザビリティのための改善
#   (上記までが ten_by_4nums.cafe の変更点)
#   03-Feb-2015 ファイル名をmake10.cafeとして計算方法を大幅に変更
#
#   使い方：
#   1. 次のようなコマンドを打つことによって，ソースから関数の定義を読み込んでおく
#       「  <$$SOURCE_FILES_DIRECTORY$ "/samples/make10.cafe" glue use  」
#   2. 4つの数値を入力．続いて「make10」関数を呼び出す．以下は「1，1，9，9」で答えを求める例
#       「  1 1 9 1 make10  」
#


# "make10" package			# to set all these definitions are defined



##########
##
##  make10 routines
##
##########

:get_operator
		"+-*/"
		swap
		nth_char
;

:try_swap
	>position
	>calc_b
	
	<calc_b < position nth_word >op
	<op "-" compare
	<op "/" compare
	or

	ifelse p
	:@
		pop
		<calc_b
		"swap" <position nth_insert_word
	;
	:@
		pop
		<calc_b
	;
;

: operations
	{
		0 >counter
		
		64 times 
		:@
			### make calculation
			<counter  1 / get_operator
			<counter  4 / get_operator
			<counter 16 / get_operator
			2 times glue
			>calc_base
			
			<calc_base
            >calc
			<calc =ppush
			
#			cl

			<calc_base "rot" 1 nth_insert_word >calc
			<calc =ppush

#			cl
			
			<calc_base
			1 try_swap 
            >calc
			<calc =ppush

#			cl

			<calc_base
			2 try_swap >calc
			<calc =ppush

#			cl

			<calc_base
			2 try_swap
			>calc_base <calc_base
			1 try_swap
			>calc
			<calc =ppush

#			cl

			<counter 1+ >counter
		;
	}
    sort uniq
;

: ten_basic
	:/ ifelse z :@ pop pop 10000; :@ "'@/'" eval ; ;

    "{" rot 
    4 times gs >four_numbers
    
    operations
    
    "[ <four_numbers ] swap gs \"}\" gs" map     # [] are not required for new 'map'

    forget /
;

:check_the_result
    >r
	<r 10 compare ifp t   ## need to use "compare" to make sure the relult is INTEGER
	:@
        [ <expr ]
        =ppush
        # "\n" gs print ^
	;
			
	<r 10.0 compare ifp t   ## need to use "compare" to make sure the relult is INTEGER
	:@
		[ <expr ] "  (fractional)" gs
		=ppush
        # "\n" gs print ^
	;
;

: make_ten_core     # takes 4 numbers as string
    >four_numbers
    {
        operations
    
        "<four_numbers swap gs" map
    
        depth times
        :@
            >expr { <expr eval }
            check_the_result
            rot
        ;
    }
;

: ten
	:/ ifelse z :@ pop pop 10000; :@ "'@/'" eval ; ;

    3 times gs
    make_ten_core

    forget /

    stack
;


##########
##
##  permutation routines
##
##########


:permutation_global_def # variable prepared as Ni ('N' is a number)
    depth >dp
    {
        1 <dp 1 - times 'dup 1 +'
        "\"i\" glue quote" map
        "\" global variable\" glue" map
        "eval" map
    }

    "g_stack" global variable
    { k} >g_stack
;

:=gpush
    >tmp <g_stack =target <tmp =parent =target pop
;

:permutation_core
    depth >dp <dp
    times
    :@
        dup <dp "i" glue >^
        >top permutation_core
        <top
        rot
    ;
    <dp 1 -
    ifp z
        '{ <1i <2i <3i <4i "gs" gather =gpush }'
;

:permutation_basic
    depth >dp <dp
    times
    :@
        " " print ^
        .
        dup >str
        >top permutation
        <top
        rot
    ;
    <dp 1 -
    ifp z               # if the depth is 1, it's a leaf call
        '<str "\n" print ^'
;

:permutation{
    permutation_global_def
    permutation_core
    <g_stack =target sort uniq
;

: make10
    permutation{ k}     # permutation calc. the result will be saved on variable "g_stack"
    cl
    {
        <g_stack

        { operations k}

        combination

        :/ ifelse z :@ pop pop 10000; :@ "'@/'" eval ; ;

        depth times
        :@
            >expr { <expr eval }
            check_the_result
            rot
        ;

        forget /
    }
    stack
;



